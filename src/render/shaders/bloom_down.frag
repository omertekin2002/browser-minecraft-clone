precision highp float;
#include <frame>
#include <common>
uniform sampler2D uSrc;
uniform vec2 uSrcTexel;
uniform int uFirst;
in vec2 vUv;
out vec4 fragColor;
vec3 S(vec2 o) {
  vec3 c = texture(uSrc, vUv + uSrcTexel * o).rgb;
  // Soft clamp keeps the sun from dominating while preserving bright sources.
  float l = luminance(c);
  return uFirst == 1 && l > 60.0 ? c * (60.0 / l) : c;
}
float kw(vec3 c) { return 1.0 / (1.0 + luminance(c)); }
void main() {
  vec3 a = S(vec2(-2, 2)), b = S(vec2(0, 2)), c = S(vec2(2, 2));
  vec3 d = S(vec2(-2, 0)), e = S(vec2(0, 0)), f = S(vec2(2, 0));
  vec3 g = S(vec2(-2, -2)), h = S(vec2(0, -2)), i = S(vec2(2, -2));
  vec3 j = S(vec2(-1, 1)), k = S(vec2(1, 1)), l = S(vec2(-1, -1)), m = S(vec2(1, -1));
  vec3 r;
  if (false) {
    vec3 g0 = (a + b + d + e) * 0.25, g1 = (b + c + e + f) * 0.25, g2 = (d + e + g + h) * 0.25, g3 = (e + f + h + i) * 0.25, g4 = (j + k + l + m) * 0.25;
    float w0 = kw(g0) * 0.125, w1 = kw(g1) * 0.125, w2 = kw(g2) * 0.125, w3 = kw(g3) * 0.125, w4 = kw(g4) * 0.5;
    r = (g0 * w0 + g1 * w1 + g2 * w2 + g3 * w3 + g4 * w4) / (w0 + w1 + w2 + w3 + w4);
  } else {
    r = e * 0.125 + (a + c + g + i) * 0.03125 + (b + d + f + h) * 0.0625 + (j + k + l + m) * 0.125;
  }
  fragColor = vec4(max(r, vec3(0.0)), 1.0);
}
